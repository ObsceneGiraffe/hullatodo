entry = { complete_flag? ~ priority? ~ date_completed? ~ date_creation? ~ tail}

complete_flag = { "x" ~ WHITE_SPACE }
priority = _{ "(" ~ priority_value ~ ")" ~ WHITE_SPACE }
priority_value = { UPPERCASE }

date_creation = { date ~ WHITE_SPACE }
date_completed = { date ~ WHITE_SPACE }

date = _{ year ~ date_sep ~ month ~ date_sep ~ day}
year = { ASCII_DIGIT{4} }
month = { ASCII_DIGIT{2} }
day = { ASCII_DIGIT{2} }
date_sep = _{ "-" }

tail = { span? ~ inner* }
inner = _{ (context_tag | project_tag | pair | span) }
span = @{(!("\n" | context_tag_prefix | project_tag_prefix | pair) ~ ANY)+ }
context_tag_prefix = @{ WHITE_SPACE ~ "@" }
project_tag_prefix = @{ WHITE_SPACE ~ "+" }
context_tag = @{ context_tag_prefix ~ tag_char+ }
project_tag = @{ project_tag_prefix ~ tag_char+ }
tag_char = { !(WHITE_SPACE) ~ ANY }

pair = { WHITE_SPACE ~ key ~ pair_sep ~ value}
key = { (!(pair_sep | WHITE_SPACE) ~ ANY)* }
pair_sep = _{ ":" }
value = { (!(WHITE_SPACE) ~ ANY)* }